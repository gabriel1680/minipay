plugins {
    id 'java'
}

version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.vavr:vavr:0.10.4'
    implementation('org.yaml:snakeyaml:2.0') {
        version { strictly("2.0") }
    }

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.0.0'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

// Test Tasks

def testGroup = 'verification'

tasks.named("test", Test) {
    useJUnitPlatform()
}

tasks.register('unitTests', Test) {
    group = testGroup
    description = 'Runs all unit tests of project'

    useJUnitPlatform {
        includeTags("unitTest")
    }
}

tasks.register('integrationTests', Test) {
    group = testGroup
    description = 'Runs all integration tests of project'

    useJUnitPlatform {
        includeTags("integrationTest")
    }
}

tasks.register('e2eTests', Test) {
    group = testGroup
    description = 'Runs all e2e tests of project'

    useJUnitPlatform {
        includeTags("e2eTest")
    }
}